plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        create("client 2") {
            client()

            name = "Second Client"
            programArgs("--username", "AnotherDev")
        }
        create("datagen") {
            data()

            name = "Minecraft Data"
            programArgs("--all", "--mod", "satchels")
            programArgs("--output", project(":common").file("src/generated/resources").absolutePath)
            programArgs("--existing", project(":common").file("src/main/resources").absolutePath)

            environmentVariable("DATAGEN", "TRUE")
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = "NeoForged"
        url = "https://maven.neoforged.net/releases"
    }
    maven {
        name = "Curios"
        url = "https://maven.theillusivec4.top/"
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    include forgeRuntimeLibrary("blue.endless:jankson:$rootProject.jankson_version")

    // We have the mod implementation here so we can mixin-to the Curios screen and render the satchel there
    modImplementation "top.theillusivec4.curios:curios-neoforge:$rootProject.curios_version+$rootProject.minecraft_version"
    compileOnly "top.theillusivec4.curios:curios-neoforge:$rootProject.curios_version+$rootProject.minecraft_version:api"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
    atAccessWideners.add("satchels.accesswidener")
}
